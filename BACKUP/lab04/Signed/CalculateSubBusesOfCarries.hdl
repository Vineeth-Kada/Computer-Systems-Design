CHIP CalculateSubBusesOfCarries {
    IN c0, P[4], G[4];
    OUT out[4];
    PARTS:
    
    /* This chip is made because this type of behaviour occurs at many places in calculating carries*/
    
    /* c1 = P0c0 + G0*/
    And(a=P[0], b=c0, out=w1);
    Or(a=w1, b=G[0], out=out[0]);
    
    /* c2 = P1P0c0 + P1G0 + G1*/
    And5Way(a=P[1], b=P[0], c=c0, d=true, e=true, out=w2);
    And(a=P[1], b=G[0], out=w3);
    Or5Way(a=w2, b=w3, c=G[1], d=false, e=false, out=out[1]);
    
    /* c3 = P2P1P0c0 + P2P1G0 + P2G1 + G2 */
    And5Way(a=P[2], b=P[1], c=P[0], d=c0, e=true, out=w4);
    And5Way(a=P[2], b=P[1], c=G[0], d=true, e=true, out=w5);
    And(a=P[2], b=G[1], out=w6);
    Or5Way(a=w4, b=w5, c=w6, d=G[2], e=false, out=out[2]);
    
    /* c4 = P3P2P1P0c0 + P3P2P1G0 + P3P2G1 + P3G2 + G3*/
    And5Way(a=P[3], b=P[2], c=P[1], d=P[0], e=c0, out=w7);
    And5Way(a=P[3], b=P[2], c=P[1], d=G[0], e=true, out=w8);
    And5Way(a=P[3], b=P[2], c=G[1], d=true, e=true, out=w9);
    And(a=P[3], b=G[2], out=w10);
    Or5Way(a=w7, b=w8, c=w9, d=w10, e=G[3], out=out[3]);
}
